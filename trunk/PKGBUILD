# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers
pkgver=4.15.0
pkgrel=1
pkgdesc='Kernel headers sanitized for use in userspace'
arch=(riscv)
url='http://www.gnu.org/software/libc'
license=(GPL2)
_commit=7501d87f7ebf8337ab2efa3fe692612a3b845c6f
source=(https://github.com/sifive/riscv-linux/archive/${_commit}.zip)
md5sums=('8692605b045341c13e0533ee73dd8595')

build() {
  cd ${srcdir}/riscv-linux-${_commit}

  make ARCH=riscv mrproper
  make ARCH=riscv headers_check
}

package() {
  cd ${srcdir}/riscv-linux-${_commit}
  make ARCH=riscv INSTALL_HDR_PATH="$pkgdir/usr" headers_install

  # use headers from libdrm
  rm -r "$pkgdir/usr/include/drm"
  
  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -o -name ..install.cmd \) -delete
}
